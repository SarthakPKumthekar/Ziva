#!/usr/bin/env groovy

pipeline {
    agent none
    options {
        skipDefaultCheckout true
        timestamps()
        timeout(time: 2, unit: 'HOURS') // long in case it needs to wait for machines
    }

    stages {
        stage('get sources') {
            agent { label 'git && linux' }  // must checkout to a linux machine to avoid line ending issue
            steps {
                echo 'running checkout ziva-vfx-utils stage'

                dir('ziva-vfx-utils') {
                    script {
                        scmVars = checkout scm
                    }
                }
                stash(name: 'main', includes: 'ziva-vfx-utils/**')
                stash(name: 'tools', includes: 'ziva-vfx-utils/tests/CmtTests/bitbucket-notify.py')
            }
            post {
                success {
                    script {
                        notifyBitbucket('INPROGRESS')
                    }
                }
                cleanup { cleanWs() }
            }
        }

        stage("Parallel Run Test") {
            matrix {
                axes {
                    axis {
                        name 'platform'
                        values 'windows', 'linux'
                    }
                    axis {
                        name 'mayaVersion'
                        values '2019', '2020', '2022', '2023'
                    }
                    axis {
                        name 'pythonVersion'
                        values 'py', 'py2'
                    }
                }
                excludes {  // 'py2' parameter is only for Maya 2022
                    exclude {
                        axis {
                            name 'platform'
                            values 'windows', 'linux'
                        }
                        axis {
                            name 'mayaVersion'
                            values '2019', '2020', '2023'
                        }
                        axis {
                            name 'pythonVersion'
                            values 'py2'
                        }
                    }
                }
                stages {
                    stage("Run tests") {
                        agent { label "${platform} && vfx && nogpu" }
                        environment {
                            mayaVersion = "${mayaVersion}"
                        }
                        steps {
                            echo "======== Platform - ${platform}, Maya ${mayaVersion}, ${pythonVersion} ========"

                            timeout(time: 15, unit: 'MINUTES') {
                                unstash 'main'
                                script {
                                    if ("${platform}" == 'windows') {
                                        runWindowsTestStage()
                                    }
                                    else if ("${platform}" == 'linux') {
                                        if ("${mayaVersion}" == '2022' && "${pythonVersion}" == 'py2') {
                                            runLinuxTestStage(true)
                                        } else {
                                            runLinuxTestStage(false)
                                        }
                                    }
                                }
                            }
                        }
                        post { cleanup { cleanWs() } }
                    }
                }
            }
        }

        stage('doc generation') {
            agent { label 'cent7 && vfx && nogpu' }
            steps {
                timeout(time: 15, unit: 'MINUTES') {
                    unstash 'main'
                    sh "ziva-vfx-utils/docs/build_the_docs.sh"
                }
            }
            post { cleanup { cleanWs() } }
        }
    }

    post{
        unsuccessful {
            node ('git && linux') {
                unstash 'tools'
                script {
                    notifyBitbucket('FAILED')
                }
            }
        }
        success {
            node ('git && linux') {
                unstash 'tools'
                script {
                    notifyBitbucket('SUCCESSFUL')
                }
            }
        }
    }
}

def runWindowsTestStage() {
    echo "running windows test stage"

    environment {
        zivadyn_LICENSE="${env.zivadyn_LICENSE};C:\\ziva"
    }

    dir('ziva-vfx-utils') {
        bat "python tests/CmtTests/copy_plugin_zBuilder_tests.py --maya ${mayaVersion}"
        bat "python tests/CmtTests/run_zbuilder_tests.py --maya ${mayaVersion}"
    }
}

def runLinuxTestStage(runInPython2Env) {
    echo "running linux test stage"

    dir('ziva-vfx-utils') {
        withEnv([
          "zivadyn_LICENSE=${env.zivadyn_LICENSE}:/var/ziva"
        ]) {
            sh "python tests/CmtTests/copy_plugin_zBuilder_tests.py --maya ${mayaVersion}"
            if (runInPython2Env) {
                sh "python tests/CmtTests/run_zbuilder_tests.py --maya ${mayaVersion} --py2"
            } else {
                sh "python tests/CmtTests/run_zbuilder_tests.py --maya ${mayaVersion}"
            }
        }
    }
}

// Call our Bitbucket notification script to tell Bitbucket to update the status of the branch being built.
// status should be "SUCCESSFUL", "INPROGRESS", or "FAILED"
def notifyBitbucket(String status) {
    withCredentials([usernamePassword( credentialsId: 'ZivaBuild', usernameVariable: 'USR', passwordVariable: 'PWS' )]) {
        // String interpoloation in Jenkins-Groovy is garbage. Single-quotes do NOT interpolate $-strings
        // so we use them for variables that shouldn't be expanded until shell execution-time.
        // Double-quotes DO interpolate $-strings, so we use them for variables we want to eagerly
        // expand right now during Jenkins script evaluation. Then we concatenate together the
        // single-quote and double-quote strings to build the final shell command we want executed.
        sh 'python ${WORKSPACE}/ziva-vfx-utils/tests/CmtTests/bitbucket-notify.py "zBuilder" ' + "\"${status}\" \"${scmVars.GIT_COMMIT}\"" + ' $BUILD_NUMBER $BUILD_URL ${USR} ${PWS}'
    }
}
